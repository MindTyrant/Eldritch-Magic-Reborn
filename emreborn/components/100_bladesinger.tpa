/** @tra setup.tra */
ACTION_IF GAME_IS ~bgee bg2ee eet~ BEGIN

      LAF ADD_KIT_EX
            INT_VAR     // Mage	            = 1
                        // Fighter              = 2
                        // Cleric               = 3
                        // Thief                = 4
                        // Bard                 = 5
                        // Paladin              = 6
                        // Fighter/Mage         = 7
                        // Fighter/Cleric       = 8
                        // Fighter/Thief        = 9
                        // Fighter/Mage/Thief   = 10
                        // Druid                = 11
                        // Ranger               = 12
                        // Mage/Thief           = 13
                        // Cleric/Mage          = 14
                        // Cleric/Thief         = 15
                        // Fighter/Druid        = 16
                        // Fighter/Mage/Cleric  = 17
                        // Cleric/Ranger        = 18
                        // Sorcerer             = 19
                        // Monk                 = 20
                        // Shaman               = 21
                        kit_class   = 7
                        
                        mixed       = RESOLVE_STR_REF(@100)
                        
                        lower       = RESOLVE_STR_REF(@101)
                        
                        help        = RESOLVE_STR_REF(@102)
                        
                        briefdesc   = RESOLVE_STR_REF(@103)

            STR_VAR     kit_name    = ~em_bladesinger~

                        unusable    = ~0x00004000~

                        clasweap    = ~4 1 1 1 1 1 1 1~
                        //             L S B S B S A M
                        //             A M O P L P X I
                        //             R A W E U I E S B   S   2   S   W       F   1 C   S       S
                        //             G L   A N K   S A L H   H   M   A     H L   / R L H     2 W
                        //             E L   R T E   I S O O   A K T D R     A A   4 O O O     H D S
                        //             S S       D   L T N R   N A R A H   S L I   S S N R   S A & G
                        //             W W           E D G T   D T W G A C P B L M T S G T D L N S L 2
                        //             D D             S S S A S A A G M L E E M A A B B B A I D H W W
                        //                             W W W X W N K E M U A R R C F O O O R N E L P P
                        //                             D D D E D A I R R B R D N E F W W W T G D D N N
                        weapprof    = ~4 1 1 1 1 1 1 1 1 4 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 1 0 1~
                        
                        abclasrq    = ~13 15 13 15 3 8~

                        // abclsmod = ~0 0 0 0 0 0~
                        
                        // abdcdsrq = ~0 0 0 0 0 0~
                        
                        // abdcscrq = ~0 0 0 0 0 0~

                        //             L L L N N N C C C
                        //             G N E G N E G N E
                        alignmnt    = ~1 1 1 1 0 0 1 0 0~

                        // dualclas = ~0 0 0 0 0 0~

                        luabbr      = ~em0~
                        
                        // stweap      = ~CHAN16 * * BAG29 RING06 EMBS07 * EMBS10 AMUL21 BRAC16 BELT10 AROW11,40 AROW11,40 AROW11,40 POTN52,5 POTN04,2 POTN14,5 * * SW1H73~
                        
                        clab_path   = ~emreborn/2da/clabem01.2da~
                        
                        kittable    = ~K_FM_E~
                        
                        base_class  = F

                        // clab_base_f = 
                        
                        // clab_base_p = 
                        
                        // clab_base_d = 
                        
                        // clab_base_r = 
                        
                        // clab_base_m = 
                        
                        // clab_base_t = 

                        //                 H   H   H
                        //             H E E D L G O
                        clsrcreq    = ~0 1 0 0 0 0 0~
                        
                        clswpbon    = ~1 0 3~

                        // hpclass     = ~HPFM~

                        numwslot    = ~2~
                        
                        clascolr    = ~208 221 220 213 246~
                        
                        clasiskl    = ~0 0 0 0 0 0 0~
                        
                        // clasthac = ~0~
                        
                        // thiefscl = ~0 0 0 0 0 0 0 0~
                        
                        // backstab = ~0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
                        
                        // sneakatt = ~0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
                        
                        // crippstr = ~0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0 0~
                        
                        // thiefskl = ~0 0~
                        
                        // traplimt = ~6~
                        
                        // bdstweap = ~~
            RET   kit_id
      END
      
      // Bladesongs
      COPY ~%MOD_FOLDER%/bam/embs1.bam~ ~override~
            LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@110) STR_VAR bam_file = embs1 RET embs1 = icon END 
      COPY  ~%MOD_FOLDER%/spl/embs1.spl~ ~override~
            SAY NAME1 @110
            SAY UNIDENTIFIED_DESC @111
            LPF ALTER_EFFECT INT_VAR match_opcode = 142 parameter2 = embs1 END      
      COPY ~%MOD_FOLDER%/bam/embs2.bam~ ~override~
            LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@114) STR_VAR bam_file = embs2 RET embs2 = icon END
      COPY  ~%MOD_FOLDER%/spl/embs2.spl~ ~override~
            SAY NAME1 @114
            SAY UNIDENTIFIED_DESC @115
            LPF ALTER_EFFECT INT_VAR match_opcode = 142 parameter2 = embs2 END
      COPY ~%MOD_FOLDER%/bam/embs3.bam~ ~override~
            LAF cd_new_portrait_icon INT_VAR string = RESOLVE_STR_REF(@118) STR_VAR bam_file = embs3 RET embs3 = icon END
      COPY  ~%MOD_FOLDER%/spl/embs3.spl~ ~override~
            SAY NAME1 @118
            SAY UNIDENTIFIED_DESC @119
            LPF ALTER_EFFECT INT_VAR match_opcode = 142 parameter2 = embs3 END

      // Adds Bladesinger's other CLAB spells.
      COPY  ~%MOD_FOLDER%/spl/emcl10.spl~ ~override~
            SAY NAME1 @120
            SAY UNIDENTIFIED_DESC @121      
      COPY  ~%MOD_FOLDER%/spl/emcl11.spl~ ~override~ //No name for this spell, as it is a stat adjustment spell.
            SAY UNIDENTIFIED_DESC @122
      COPY  ~%MOD_FOLDER%/spl/emcl12.spl~ ~override~
            SAY NAME1 @123
            SAY UNIDENTIFIED_DESC @124
      COPY  ~%MOD_FOLDER%/spl/emcl13.spl~ ~override~
            SAY NAME1 @125
            SAY UNIDENTIFIED_DESC @126
      COPY  ~%MOD_FOLDER%/spl/emcl14.spl~ ~override~
            SAY NAME1 @127
            SAY UNIDENTIFIED_DESC @128       
      COPY  ~%MOD_FOLDER%/spl/emcl15.spl~ ~override~
            SAY NAME1 @129
            SAY UNIDENTIFIED_DESC @130

      //Reduces proficiences; does so for classes, but not kits. So it will affect all F/M, not just Bladesingers.  Thankfully, there are no vanilla F/M companions. Do not use with Khalid or Xan as F/M mod.
      COPY_EXISTING ~profs.2da~ ~override~
            COUNT_2DA_ROWS 3 rowcount
            FOR (row=0;row < rowcount;++row) BEGIN
                  READ_2DA_ENTRY row 0 3 this_class
                  PATCH_IF "%this_class%" STR_EQ "FIGHTER_MAGE" BEGIN   // we found our class
                        SET_2DA_ENTRY row 1 3 2                         //Set FIRST_LEVEL to 2
                        SET_2DA_ENTRY row 2 3 4                         //Set RATE to 4
                        row=rowcount                                    //Exit loop after finding match
                  END
            END
      BUT_ONLY

      // Makes sure Bladesinger's clab table is installed
      ACTION_IF (NOT FILE_EXISTS_IN_GAME ~clabem01.2da~) BEGIN
            COPY ~%MOD_FOLDER%/2da/clabem01.2da~ ~override~
      END

      // Makes sure Bladesinger's High-Level Ability file is installed
      ACTION_IF (NOT FILE_EXISTS_IN_GAME ~luem0.2da~) BEGIN
            COPY ~%MOD_FOLDER%/2da/luem0.2da~ ~override~
      END

      //Adds Story-related items.
      COPY  ~%MOD_FOLDER%/spl/embs07.spl~ ~override~ // Bladesinger's Signet
            SAY 0xfe @130
      COPY  ~%MOD_FOLDER%/itm/embs07.itm~ ~override~ 
            SAY NAME1 @132
            SAY NAME2 @133
            SAY UNIDENTIFIED_DESC @134
            SAY IDENTIFIED_DESC @135
            SAY 0x76 @136
            WRITE_LONG 0x1c6 IDS_OF_SYMBOL (KIT em_bladesinger)
            SAY 0x1ee @139
      COPY_EXISTING "RING40.ITM" ~override~
            LPF ADD_ITEM_EQEFFECT   INT_VAR opcode = 180 target = 1 parameter1 = RESOLVE_STR_REF(@136) parameter2 = 0 timing = 2 resist_dispel = 2    STR_VAR resource = ~embs07~   END
      BUT_ONLY IF_EXISTS
      COPY  ~%MOD_FOLDER%/spl/embs10.spl~ ~override~ // Elven Boots of Speed: The Paws of the Lythari
            SAY 0xfe @140
      COPY  ~%MOD_FOLDER%/itm/embs10.itm~ ~override~ 
            SAY NAME1 @142
            SAY NAME2 @143
            SAY UNIDENTIFIED_DESC @144
            SAY IDENTIFIED_DESC @145
            WRITE_LONG 0x106 IDS_OF_SYMBOL (KIT em_bladesinger)
            SAY 0x12e @139
      ACTION_IF (MOD_IS_INSTALLED ~emreborn.tp2~ ~10~) THEN BEGIN
            COPY  ~%MOD_FOLDER%/itm/emchan16.itm~ ~override~ //New Unique Bladesong Chain +4 
                  SAY NAME1 @150
                  SAY NAME2 @151
                  SAY UNIDENTIFIED_DESC @152
                  SAY IDENTIFIED_DESC @153
                  SAY 0x346 @12
                  SAY 0x376 @13
                  WRITE_LONG 0x4c6 IDS_OF_SYMBOL (KIT em_bladesinger)      
                  SAY 0x4ee @139
            COPY_EXISTING "dragblac.cre" ~override~
                  REPLACE_CRE_ITEM "emchan16.itm" #0 #0 #0 NONE INV6
            BUT_ONLY IF_EXISTS
            COPY  ~%MOD_FOLDER%/itm/emchan19.itm~ ~override~ //New Cespenar Chain +5
                  SAY NAME1 @156
                  SAY NAME2 @157
                  SAY UNIDENTIFIED_DESC @158
                  SAY IDENTIFIED_DESC @159
                  SAY 0x256 @161
                  SAY 0x286 @162
                  SAY 0x2b6 @163
                  SAY 0x2e6 @164
                  SAY 0x316 @165
                  WRITE_LONG 0x496 IDS_OF_SYMBOL (KIT em_bladesinger)      
                  SAY 0x4be @166
            COPY_EXISTING ~botsmith.dlg~ ~override~
                  DECOMPILE_AND_PATCH BEGIN
                        REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~CHAN16~ ~emchan16~
                        REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~cHAN19~ ~emchan19~
                        REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~SCRL6T~ ~SCRL7O~
                        REPLACE_TEXTUALLY CASE_INSENSITIVE EXACT_MATCH ~Gold(40000)~ ~Gold(80000)~            
                  END
            BUT_ONLY IF_EXISTS
            OUTER_SPRINT recipe_short @168
            OUTER_SPRINT recipe_tweak @169
            STRING_SET_EVALUATE 66897 ~%recipe_short%~
            STRING_SET_EVALUATE 66898 ~%recipe_tweak%~
      END
      COPY ~%MOD_FOLDER%/spl/embs20.spl~ ~override~ // Artblade +4
            SAY 0xfe @180 
      COPY ~%MOD_FOLDER%/eff/emholy.eff~ ~override~
      COPY ~%MOD_FOLDER%/itm/embs20.itm~ ~override~
            SAY NAME1 @182
            SAY NAME2 @183
            SAY UNIDENTIFIED_DESC @185
            SAY IDENTIFIED_DESC @186
            WRITE_LONG 0x396 IDS_OF_SYMBOL (KIT em_bladesinger)
            SAY 0x3be @187
      APPEND ITEMSPEC.2DA ~embs20   1~ UNLESS ~embs20~
      COPY  ~%MOD_FOLDER%/dlg/embs20.dlg~ ~override~
            SAY 0x34  @188
            SAY 0x44  @189
            SAY 0x54  @190
            SAY 0x64  @191
            SAY 0x74  @192
            SAY 0x84  @193
            SAY 0x98  @194
            SAY 0xb8  @195
            SAY 0xd8  @196
            SAY 0xf8  @197
            SAY 0x118 @198
      APPEND ITEMDIAL.2DA ~embs20 %TAB%5689 %TAB%embs20~ UNLESS ~embs20~

      // Stat: Proficiency [233] target = 1 (self) match_parameter2 = (134 + (0x10000 * 1))
      // Protection: Immunity Spell [318] target = 1 parameter1 = 4 parameter2 = %nosws_row% timing = 2 resist_dispel = 0
      APPEND splprot.2da ~EM_1_HAND %TAB%134 %TAB%-1 %TAB%8~ UNLESS ~EM_1_HAND~
      COPY_EXISTING ~splprot.2da~ ~nowhere~
            COUNT_2DA_COLS cols                             // amount of columns
            READ_2DA_ENTRIES_NOW rows cols                  // read all file into memory  
            FOR (row = 1; row < rows; ++row) BEGIN          // iterate over rows
                  READ_2DA_ENTRY_FORMER rows row 0 ~stat~   // read column value
                  PATCH_IF ~%stat%~ STRING_EQUAL_CASE ~EM_1_HAND~ BEGIN
                        SET nosws_row = %row%
                  END
            END

      // Spell: Protection from Spell [206] target = 1 (self) timing = 2-While equipped
      // Removal: Effects specified by Resource [321] timing = 2-While equipped
      COPY_EXISTING ~chan04.itm~ ~nowhere~
            READ_LONG NAME1 splint_strref
      COPY_EXISTING_REGEXP GLOB ~^.+\.itm$~ ~override~
            PATCH_IF (SOURCE_SIZE > 0x71) BEGIN             // skip null/broken items
                  READ_SHORT ITM_type type
	            PATCH_IF (type = 0x02)  BEGIN 		// armors; splint and plate armor remove effects of bladesongs and prevents their application
                        READ_LONG ITM_animation appearance
                        PATCH_IF (appearance = 16691) BEGIN // chain appearance
                              READ_LONG NAME1 gen_name_strref
                              PATCH_IF (gen_name_strref = splint_strref) BEGIN      // only splint, not chain
                                    LPF ADD_ITEM_EQEFFECT   INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 2   STR_VAR resource = ~embs1~ END
                                    LPF ADD_ITEM_EQEFFECT   INT_VAR opcode = 321 timing = 2                                   STR_VAR resource = ~embs1~ END
                                    LPF ADD_ITEM_EQEFFECT   INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 2   STR_VAR resource = ~embs2~ END
                                    LPF ADD_ITEM_EQEFFECT   INT_VAR opcode = 321 timing = 2                                   STR_VAR resource = ~embs2~ END
                                    LPF ADD_ITEM_EQEFFECT   INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 2   STR_VAR resource = ~embs3~ END
		                        LPF ADD_ITEM_EQEFFECT   INT_VAR opcode = 321 timing = 2                                   STR_VAR resource = ~embs3~ END
                              END
	                  END ELSE BEGIN
                              PATCH_IF (appearance = 16692) BEGIN // plate appearance
                                    LPF ADD_ITEM_EQEFFECT   INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 2   STR_VAR resource = ~embs1~ END
                                    LPF ADD_ITEM_EQEFFECT   INT_VAR opcode = 321 timing = 2                                   STR_VAR resource = ~embs1~ END
                                    LPF ADD_ITEM_EQEFFECT   INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 2   STR_VAR resource = ~embs2~ END
                                    LPF ADD_ITEM_EQEFFECT   INT_VAR opcode = 321 timing = 2                                   STR_VAR resource = ~embs2~ END
                                    LPF ADD_ITEM_EQEFFECT   INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 2   STR_VAR resource = ~embs3~ END
                                    LPF ADD_ITEM_EQEFFECT   INT_VAR opcode = 321 timing = 2                                   STR_VAR resource = ~embs3~ END
                              END
                        END
                  END ELSE BEGIN    //Shields, ranged weapons, and 2H-weapons remove effects of bladesongs and prevents their application  
                        PATCH_IF    (type = 0x0c)     OR    // shields
                                    (type = 0x0f)     OR    // bows
                                    (type = 0x12)     OR    // slings
                                    (type = 0x18)     OR    // darts
                                    (type = 0x1a)     OR    // quarterstaffs
                                    (type = 0x1b)     OR    // crossbows
                                    (type = 0x1c)     OR    // hand-to-hand
                                    (type = 0x1d)     OR    // spears
                                    (type = 0x1e)     BEGIN // halberds (2-handed polearms)
                              LPF ADD_ITEM_EQEFFECT   INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 2   STR_VAR resource = ~embs1~ END
                              LPF ADD_ITEM_EQEFFECT   INT_VAR opcode = 321 timing = 2                                   STR_VAR resource = ~embs1~ END
                              LPF ADD_ITEM_EQEFFECT   INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 2   STR_VAR resource = ~embs2~ END
                              LPF ADD_ITEM_EQEFFECT   INT_VAR opcode = 321 timing = 2                                   STR_VAR resource = ~embs2~ END
                              LPF ADD_ITEM_EQEFFECT   INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 2   STR_VAR resource = ~embs3~ END
                              LPF ADD_ITEM_EQEFFECT   INT_VAR opcode = 321 timing = 2                                   STR_VAR resource = ~embs3~ END
                        END ELSE BEGIN
                              PATCH_IF    (type = 0x19)     OR    // axes
                                          (type = 0x14)     OR	// large swords
                                          (type = 0x11)     OR	// maces and clubs
                                          (type = 0x10)     OR	// daggers
                                          (type = 0x17)     OR	// flails
                                          (type = 0x16)     OR    // morningstars
                                          (type = 0x13)     OR	// small swords
                                          (type = 0x15)     BEGIN // hammers
                                    READ_BYTE ITM_flags handed
                                    PATCH_IF ((%handed% BOR 0b11111101) = 0b11111101) BEGIN //	1-handed
                                          LPF DELETE_EFFECT INT_VAR match_opcode = 233 target = 1 match_parameter2 = (134 + (0x10000 * 1))      END
                                          LPF ADD_ITEM_EQEFFECT   INT_VAR opcode = 318 target = 1 parameter1 = 4 parameter2 = %nosws_row% timing = 2 resist_dispel = 0  STR_VAR resource = ~embs1~    END
                                          LPF ADD_ITEM_EQEFFECT   INT_VAR opcode = 318 target = 1 parameter1 = 4 parameter2 = %nosws_row% timing = 2 resist_dispel = 0  STR_VAR resource = ~embs2~    END
                                          LPF ADD_ITEM_EQEFFECT   INT_VAR opcode = 318 target = 1 parameter1 = 4 parameter2 = %nosws_row% timing = 2 resist_dispel = 0  STR_VAR resource = ~embs3~    END
                                          LPF ADD_ITEM_EQEFFECT   INT_VAR opcode = 321 timing = 0 duration = 1    STR_VAR resource = ~embs1~    END
                                          LPF ADD_ITEM_EQEFFECT   INT_VAR opcode = 321 timing = 0 duration = 1    STR_VAR resource = ~embs2~    END
                                          LPF ADD_ITEM_EQEFFECT   INT_VAR opcode = 321 timing = 0 duration = 1    STR_VAR resource = ~embs3~    END
                                          LPF ADD_ITEM_EQEFFECT   INT_VAR opcode = 233 target = 1 parameter1 = 2 parameter2 = (134 + (0x10000 * 1)) timing = 2 resist_dispel = 0    END
                                    END ELSE BEGIN
                                          LPF ADD_ITEM_EQEFFECT   INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 2   STR_VAR resource = ~embs1~ END
                                          LPF ADD_ITEM_EQEFFECT   INT_VAR opcode = 321 timing = 2     STR_VAR resource = ~embs1~ END
                                          LPF ADD_ITEM_EQEFFECT   INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 2   STR_VAR resource = ~embs2~ END
                                          LPF ADD_ITEM_EQEFFECT   INT_VAR opcode = 321 timing = 2     STR_VAR resource = ~embs2~ END
                                          LPF ADD_ITEM_EQEFFECT   INT_VAR opcode = 206 target = 1 parameter1 = (0 - 1) timing = 2   STR_VAR resource = ~embs3~ END
                                          LPF ADD_ITEM_EQEFFECT   INT_VAR opcode = 321 timing = 2     STR_VAR resource = ~embs3~ END
                                    END
                              END
                        END
                  END
            END
      BUT_ONLY 
END

PRINT ~Thank you for installing: "Bladesinger Kit"~
